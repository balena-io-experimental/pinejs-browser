define(['ometa-core'], function(OMeta) {
    ometa ClientURIUnparser{
          word = [(letter|'-'|'_')+:l]                                        -> l.join(''),
          nmbr = number,

         trans = [:t apply(t):a] -> a,
           uri   trans:t         -> ('#!/' + t),

          name = [word:w nmbr:n] -> (w + '.' + n),
          name = [word:w word:o] -> (w + '.' + o),
          name = [word:w]        -> (w),

           collection = name:n mod:m trans:t trans+:r {t=[t].concat(r)} -> (n + m + '/(' + t.join(',') + ')'),
           collection = name:n mod:m trans:t                          -> (n + m + '/' + t),
           collection = name:n mod:m                                  -> (n + m),

           instance = name:n mod:m trans:t trans+:r {t=[t].concat(r)} -> (n + m + '/(' + t.join(',') + ')'),
           instance = name:n mod:m trans:t                          -> (n + m + '/' + t),
           instance = name:n mod:m                                  -> (n + m),

           mod = ['mod' actn+:a] -> a.join('')
               | ['mod']         -> '',

          actn = (view|add|filt|del|edit),
          view = ['view']         -> '*view',
          add = ['add']          -> '*add',
          del = ['del']          -> '*del',
          edit = ['edit']         -> '*edit',

          filt = ['filt' op*:o]   -> ('*filt:' + o.join(';')),

            op = eq|ne|lk,

            eq = ['eq' ([]|word) word:field (word|nmbr):value] -> [field, value].join('='),

            ne = ['ne' ([]|word) word   (word|nmbr)],
            lk = ['lk' ([]|word) word   (word|nmbr)]
    }
    return ClientURIUnparser;
});